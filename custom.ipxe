#!ipxe

# Alpine Linux
# https://alpinelinux.org

isset ${dhcp-server} && set ipparam ip=dhcp || set ipparam ip=${ip}::${gateway}:${netmask}::eth0:none:${dns}

:start
clear alpine_version
set os Alpine Linux
set os_arch ${arch}
iseq ${os_arch} x86_64 && set os_arch x86_64 ||
iseq ${os_arch} i386 && set os_arch x86 ||
iseq ${os_arch} arm64 && set os_arch aarch64 ||
menu ${os} - ${os_arch}
item --gap Releases

item v3.8 ${space} ${os} v3.8
item v3.9 ${space} ${os} v3.9
item v3.10 ${space} ${os} v3.10
item v3.11 ${space} ${os} v3.11
item v3.12 ${space} ${os} v3.12
item v3.13 ${space} ${os} v3.13
item v3.14 ${space} ${os} v3.14
item v3.15 ${space} ${os} v3.15
item latest-stable ${space} ${os} Latest (stable)
item edge ${space} ${os} Edge (development)
choose alpine_version || goto custom_exit
goto boot

:boot
set base-url ${alpinelinux_mirror}
set dir ${alpinelinux_base_dir}/${alpine_version}/releases/${os_arch}/netboot
set repo-url ${alpinelinux_mirror}/${alpinelinux_base_dir}/${alpine_version}/main
imgfree
kernel ${base-url}/${dir}/vmlinuz-lts ${ipparam} alpine_repo=${repo-url} modules=loop,squashfs modloop=${base-url}/${dir}/modloop-lts quiet nomodeset initrd=initrd.magic ${cmdline}
initrd ${base-url}/${dir}/initramfs-lts
echo
echo MD5sums:
md5sum vmlinuz-lts initramfs-lts
boot

:custom_exit
clear menu
exit 0
